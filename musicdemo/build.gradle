appModuleMusicDemo = rootProject.ext.appModuleMusicDemo
if (Boolean.valueOf(appModuleMusicDemo)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi", "x86"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets{
        main {
            if (Boolean.valueOf(appModuleMusicDemo)) {
                manifest.srcFile 'app/AndroidManifest.xml'
                java.srcDirs = ['src/main/java','app/java']
                resources.srcDirs = ['src/main/resources','app/resources']
                aidl.srcDirs = ['src/main/aidl','app/aidl']
                renderscript.srcDirs = ['src/main/rs','app/rs']
                res.srcDirs = ['src/main/res','app/res']
                assets.srcDirs = ['src/main/assets','app/assets']
                jniLibs.srcDirs = ['src/main/jniLibs','app/jniLibs']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //排除java/debug文件夹下的所有文件
                    exclude '*debug'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
    compile project(':macore')
    annotationProcessor rootProject.ext.dependencies["butterknife_compiler"]
}
